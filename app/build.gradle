plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion ApplicationConfigration.CompileSdkVersion

    defaultConfig {
        applicationId ApplicationConfigration.ApplicationId
        minSdkVersion ApplicationConfigration.MinSdkVersion
        targetSdkVersion ApplicationConfigration.CompileSdkVersion
        versionCode ApplicationConfigration.VersionCode
        versionName ApplicationConfigration.VersionName
        testInstrumentationRunner TestInstrumentedRunner.Runner
    }

    flavorDimensions "core"

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    productFlavors {

        sharedTestingResources {
            dimension "core"
            applicationIdSuffix = ".mock"

            buildConfigField("String", "LEFT", ApplicationConfigration.EnglishName)
            buildConfigField("String", "RIGHT", ApplicationConfigration.ArabicName)
            resValue("string", "app_name", ApplicationConfigration.ApplicationName)

            buildConfigField("String", "BASE_URL", ApplicationConfigration.BaseUrl)
            buildConfigField("String", "AUTH_URL", ApplicationConfigration.AuthUrl)
            buildConfigField("String", "PRODUCTS_URL", ApplicationConfigration.ProductsUrl)

        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose = true
        dataBinding = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

}

dependencies {

    implementation KotlinLibrary.Library
    implementation AndroidLibrary.Core
    MaterialDesignLibraries.Libraries.forEach { implementation it }
    MaterialDesignLibraries.CustomViews.forEach { implementation it }
    Coroutine.Libraries.forEach { implementation it }
    // Retrofit
    NetworkLibraries.Retrofit.forEach { implementation it }
    // Jetpack
    Jetpack.LifeCycle.forEach { implementation it }
    // DI
    DI.Koin.forEach { implementation it }
    // Image Loader
    implementation ImageLoader.Glide
    annotationProcessor ImageLoader.GlideAnnotateProcessor
    // For Compose
    Compose.Libraries.forEach { implementation(it)}
    // For Local Test
    testImplementation TestingLibraries.Junit
    testImplementation TestingLibraries.Truth
    testImplementation TestingLibraries.JunitExtensions
    testImplementation TestingLibraries.Core
    TestingLibraries.MockK.forEach { testImplementation it }
    testImplementation TestingLibraries.ArcCore
    testImplementation TestingLibraries.CoroutineTest
    // For SharedSourceSet Resources That helped us for testing
    sharedTestingResourcesImplementation TestingLibraries.Junit
    sharedTestingResourcesImplementation TestingLibraries.CoroutineTest
    // For Instrumented Test
    androidTestImplementation TestingLibraries.Core
    UITestingLibraries.Espresso.forEach { androidTestImplementation it }
    androidTestImplementation UITestingLibraries.AndroidXRuleTest


}